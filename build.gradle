plugins {
    id 'java'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'io.github.rdf-connect'
version = '0.0.3'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.grpc:grpc-stub:1.63.0'
    implementation 'io.grpc:grpc-protobuf:1.63.0'
    implementation 'com.google.protobuf:protobuf-java:4.28.2'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.63.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        proto {
            srcDirs = ['proto']
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
